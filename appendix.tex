\newpage
\section*{Appendix}
\begin{center}
\textbf{Antlr code for parsing the transition system.}
\end{center}

\begin{lstlisting}
grammar TransitionSystem;
options { language = Java; }

@header {
package modelchecker.parserTS;
import modelchecker.astTS.*;
import java.util.LinkedList; }

@lexer::header {
package modelchecker.parserTS; }

system returns [TransitionSystem value]
  : e=states EOF  {$value = new TransitionSystem(e); } ;

states returns [LinkedList<State> value]
  : s=state {$value = new LinkedList<State>(); $value.add(s); } (s=state {$value.add(s); } )*
  |         {$value = new LinkedList<State>(); } ;

state returns [State value]
  :  '{' s=state_info '[' a=atom_props ']' e=edges '}' {$value = new State(s, a, e); } ;

state_info returns [StateInfo value]
 :  S '*'   {$value = new StateInfo(Integer.parseInt($S.getText()), true); }
 |  S       {$value = new StateInfo(Integer.parseInt($S.getText()), false); } ;

atom_props returns [LinkedList<String> value]
  : a=atom    {$value = new LinkedList<String>(); $value.add(a); } (',' a=atom {$value.add(a); } )*
  |    {$value = new LinkedList<String>(); } ;

atom returns [String value]
  : P  {$value = $P.getText(); } ;

edges returns [LinkedList<Integer> value]
  : e=edge        {$value = new LinkedList<Integer>(); $value.add(e); }  ( e=edge      {$value.add(e); } )*
  |               {$value = new LinkedList<Integer>(); } ;

edge returns [Integer value]
  :  S    {$value = Integer.parseInt($S.getText()); } ;

S : '0'..'9'+ ;
P  : 'a'..'z'+ ;
WS  :   (' '|'\t'|'\r'|'\n')+ { $channel = HIDDEN; } ; 
\end{lstlisting}
 
 \newpage
 
 \begin{center}
\textbf{Antlr code for parsing the formula.}
\end{center}
 
\begin{lstlisting}
grammar FormulaChecker;

options { language = Java; }

@header {
package modelchecker.parserF;
import modelchecker.astF.*; }

@lexer::header {
package modelchecker.parserF; }

formula returns [Formula value]
  : p=phi  EOF  {$value = new Formula(p); } ;

phi returns [Phi value]
  : 'TT' 	{$value = new Phi("tt"); }
  |  AP 	{$value = new Phi($AP.getText()); }
  | 'EX['ex=phi']' 	{$value = new EXPhi(ex); }
  | 'EF['ef=phi']' 	{$value = new EFPhi(ef); }
  | 'AX['ax=phi']' 	{$value = new AXPhi(ax); }
  | 'AG['ag=phi']' 	{$value = new AGPhi(ag); }
  | 'NOT['not=phi']' 	{$value = new NOTPhi(not); }
  | 'AND['a1=phi']['a2=phi']' {$value = new ANDPhi(a1, a2); } ;

AP  : 'a'..'z'+ ;
WS  :   (' '|'\t'|'\r'|'\n')+ { $channel = HIDDEN; } ; 

\end{lstlisting}

\newpage

\begin{center}
\textbf{Java code, an example of a class in the abstract syntax tree for formulas.}
\end{center}

\begin{lstlisting}
public class EXPhi extends Phi{

  public EXPhi(Phi phi){
      super(phi);
  }

  @Override
  public HashMap<Integer, State> evaluate(EnvironmentF env) {
      return ctlEX(env.getStates(),phi.evaluate(env))  ;
  }

  private HashMap<Integer, State> ctlEX(HashMap<Integer, State> states, HashMap<Integer, State> satPhi) {
      HashMap<Integer, State> matches = new HashMap<>();
  
      loop: for (Integer key1 : states.keySet()) {
          State state = states.get(key1);

          for (int target : state.targets) {

              for (Integer key2 : satPhi.keySet()) {
                  State end = satPhi.get(key2);

                  if (target == end.stateNr) {
                      matches.put(state.stateNr, state);
                      continue loop;
                  }
              }
          }
      }
      return matches;
  }

}
\end{lstlisting}